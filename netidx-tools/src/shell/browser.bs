use tui;
use tui::table;
use tui::block;
use tui::input_handler;

let path = "/";
let (mode, structure) = select net::list_table(path) {
    { columns, rows } => {
        let mode = uniq(select columns {
            [] => `List,
            _ => `Table
        });
        let columns: Array<string> = select array::map(columns, |(col, _)| col) {
            [] => ["Value"],
            cols => array::sort(#numeric:true, cols)
        };
        let rows: Array<string> = {
            let base = select str::dirname(rows[0]?) {
                null as _ => "/",
                s => s
            };
            let names = array::filter_map(rows, |r| str::basename(r));
            let names = array::sort(#numeric:true, names);
            array::map(names, |name| select base {
                "/" => "/[name]",
                base => "[base]/[name]"
            })
        };
        (mode, { columns, rows })
    }
};
let size: Size = never();
let selected = { x: 1, y: 0 };
let viewport = { x: 0, y: 0 };
let visible_rows = {
    let len = array::len(structure.rows);
    let start = min(len - 1, viewport.y);
    let end = min(len, viewport.y + size.height - 1);
    (structure.rows)[start .. end]?
};
let { total: _, cols: visible_columns, widths }:
    { total: i64, cols: Array<string>, widths: Array<Constraint> } = {
    let init = { total: 4, cols: [], widths: [`Min(4)] };
    array::fold((structure.columns)[viewport.x ..]?, init, |{total, cols, widths}, col| {
        let len = max(10, str::len(col));
        select len + total {
            l if l <= size.width => {
                total: l,
                cols: array::push(cols, col),
                widths: array::push(widths, `Min(len))
            },
            _ => {total, cols, widths}
        }
    })
};
let header = select mode {
    `List => row(#style:style(#bg:`DarkGray), [cell(line("Name")), cell(line("Value"))]),
    `Table => {
        let cols = array::map(visible_columns, |col| cell(line(col)));
        row(#style:style(#bg:`DarkGray), array::push_front(cols, cell(line("Name"))))
    }
};
let selected_path = {
    let row = visible_rows[selected.y]?;
    select mode {
        `List => "[row]",
        `Table => "[row]/[visible_columns[selected.x - 1]?]"
    }
};
let rows = array::map(visible_rows, |row| {
    let name = select str::basename(row) { null as _ => "", s => s };
    &tui::table::row(select mode {
        `List => [
            cell(line(name)),
            cell(line("[any("#SUB", net::subscribe("[row]"))]"))
        ],
        `Table => {
            let a = array::map(visible_columns, |col| {
                let v = any("#SUB", net::subscribe("[row]/[col]"));
                cell(line("[v]"))
            });
            let name = cell(#style:style(#bg:`DarkGray), line(name));
            array::push_front(a, name)
        }
    })
});
let handle_event = |e: Event| -> [`Stop, `Continue] select e {
    `Key(e) => select e.code {
        e@ `Left => {
            select e ~ selected.x {
                e if e <= 1 =>
                    viewport <- uniq(e ~ { viewport with x: max(0, viewport.x - 1) }),
                e => selected <- e ~ { selected with x: selected.x - 1 }
            };
            `Stop
        },
        e@ `Right => {
            let limit = array::len(visible_columns);
            let clim = array::len(structure.columns) - array::len(visible_columns);
            select e ~ selected.x {
                x if x >= limit =>
                    viewport <- uniq(x ~ { viewport with x: min(clim, viewport.x + 1) }),
                e => selected <- e ~ {selected with x: selected.x + 1}
            };
            `Stop
        },
        e@ `Down => {
            let rlim = array::len(structure.rows) - array::len(visible_rows);
            let limit = array::len(visible_rows) - 1;
            select e ~ selected.y {
                x if x >= limit =>
                    viewport <- uniq(x ~ { viewport with y: min(rlim, viewport.y + 1) }),
                e => selected <- e ~ { selected with y: selected.y + 1 }
            };
            `Stop
        },
        e@ `Up => {
            select e ~ selected.y {
                e if e <= 0 =>
                    viewport <- uniq(e ~ { viewport with y: max(0, viewport.y - 1) }),
                e => selected <- e ~ { selected with y: selected.y - 1 }
            };
            `Stop
        },
        e@ `Enter => {
            path <- e ~ (structure.rows)[selected.y]?;
            selected <- e ~ { x: 1, y: 0 };
            viewport <- e ~ { x: 0, y: 0 };
            `Stop
        },
        e@ `Backspace => select e ~ str::dirname(path) {
            null as _ => `Stop,
            s => {
                path <- s;
                selected <- s ~ { x: 1, y: 0 };
                viewport <- s ~ { x: 0, y: 0 };
                `Stop
            }
        },
        _ => `Continue
    },
    _ => `Continue
};
let grid = table(
    #cell_highlight_style:&style(#bg:`Yellow),
    #header:&header,
    #widths:&[],
    #selected_cell: &selected,
    &rows
);
let surround = block(
    #border:&`All,
    #border_style:&style(#fg:`Green),
    #title:&line(path),
    #title_bottom:&line(selected_path),
    #size:&size,
    &grid
);
input_handler(#handle: &handle_event, &surround)
