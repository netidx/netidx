use tui;
use tui::table;
use tui::input_handler;

let path = "/";
dbg(path);
let structure = net::list_table(path);
// CR if I put the uniq on the struct ref it doesn't work
let mode = uniq(select structure.columns {
    [] => `List,
    _ => `Table
});
dbg(mode);
let selected = { x: 1, y: 0 };
let visible_range = {
    row: {
        min: max(0, selected.y - size.rows),
        max: selected.y + size.rows
    },
    column: {
        min: max(0, selected.x - size.columns),
        max: selected.x + size.columns
    }
};
//let is_visible = |row: i64, col: i64| uniq(
//    (row >= (visible_range.row).min) &&
//    (row <= (visible_range.row).max) &&
//    (col >= (visible_range.column).min) &&
//    (col <= (visible_range.column).max)
//);
let is_visible = |row: i64, col: i64| any(row, col) ~ true;
let columns = {
    let cols = array::map(structure.columns, |(col, _)| col);
    select cols {
        [] => ["Name", "Value"],
        cols => array::push_front(cols, "Name")
    }
};
let header = select mode {
    `List => row([cell(line("Name")), cell(line("Value"))]),
    `Table => row(array::map(columns, |col| cell(line(col))))
};
let widths: Array<Constraint> = select mode {
    `List => [`Percentage(20), `Percentage(80)],
    `Table => array::map(columns, |col| `Min(str::len(col)))
};
dbg(structure.rows);
let rows = array::map(array::enumerate(structure.rows), |(i, row)| {
    let name = select str::basename(row) { null as _ => "", s => s };
    let name = cell(line(name));
    &tui::table::row(select mode {
        `List => [ name, cell(line("[any("#SUB", net::subscribe("[row]"))]")) ],
        `Table => array::map(array::enumerate(columns), |(i, col)| select i {
            0 => name,
            _ => cell(line("[any("#SUB", net::subscribe("[row]/[col]"))]"))
        })
    })
});
let handle_event = |e: Event| -> [`Stop, `Continue] select e {
    `Key(e) => select e.code {
        e@ `Left => {
            selected <- e ~ { selected with x: max(1, selected.x - 1) };
            `Stop
        },
        e@ `Right => {
            let x = min(array::len(columns) - 1, selected.x + 1);
            selected <- e ~ { selected with x };
            `Stop
        },
        e@ `Down => {
            let y = min(array::len(structure.rows) - 1, selected.y + 1);
            selected <- e ~ { selected with y };
            `Stop
        },
        e@ `Up => {
            selected <- e ~ { selected with y: max(0, selected.y - 1) };
            `Stop
        },
        e@ `Enter => {
            path <- e ~ (structure.rows)[selected.y]?;
            selected <- e ~ { x: 1, y: 0 };
            `Stop
        },
        e@ `Backspace => select e ~ str::dirname(path) {
            null as _ => `Stop,
            s => {
                path <- s;
                selected <- s ~ { x: 1, y: 0 };
                `Stop
            }
        },
        _ => `Continue
    },
    _ => `Continue
};
input_handler(
    #handle: &handle_event,
    &table(
        #cell_highlight_style:&style(#bg:`Yellow),
        #header:&header,
        #flex:&`Center,
        #widths: &widths,
        #selected_cell: &selected,
        &rows
    )
)
