use gui;
use gui::list;
use gui::block;
use gui::input_handler;

let items = [
  line("Apple"),
  line("Banana"),
  line("Cherry"),
  line("Date"),
  line("Elderberry"),
  line("Fig"),
  line("Grape"),
  line("Honeydew"),
  line("Jackfruit"),
  line("Kiwi"),
  line("Lemon"),
  line("Mango"),
  line("Nectarine"),
  line("Orange"),
  line("Papaya"),
  line("Quince"),
  line("Raspberry"),
  line("Strawberry"),
  line("Tangerine"),
  line("Ugli fruit"),
  line("Vanilla bean"),
  line("Watermelon"),
  line("Xigua"),
  line("Yellow passion fruit"),
  line("Zucchini"),
  line("Pork")
];

let last = array::len(items) - 1;
let visible = 12;
let selected = 0;
let scroll_pos = 0;
scroll_pos <- select selected {
    s if s < scroll_pos => s,
    s if s > (scroll_pos + visible) => s - visible,
    _ => never()
};

let title_bottom = {
   let cur = items[selected]?;
   line("Selected: [cur.spans]")
};

let handle_event = |e: Event| -> [`Stop, `Continue] select e {
    `Key(k) => select k.code {
        k@`Up if selected > 0 => {
            selected <- (k ~ selected) - 1;
            k ~ `Stop
        },
        k@`Down if selected < last => {
            selected <- (k ~ selected) + 1;
            k ~ `Stop
        },
        k@`Home => {
            selected <- k ~ 0;
            k ~ `Stop
        },
        k@`End => {
            selected <- k ~ last;
            k ~ `Stop
        },
        k@`PageUp if selected < visible => {
            selected <- k ~ 0;
            k ~ `Stop
        },
        k@`PageUp => {
            selected <- (k ~ selected) - visible;
            k ~ `Stop
        },
        k@`PageDown if selected > 14 => {
            selected <- k ~ last;
            k ~ `Stop
        },
        k@`PageDown => {
            selected <- (k ~ selected) + visible;
            k ~ `Stop
        },
        k => k ~ `Continue
    },
    e => e ~ `Continue
};

input_handler(
    #handle: &handle_event,
    &block(
        #border: &`All,
        #title: &line("Fruit Selection (Use Arrow Keys, Home, End, PageUp, PageDown)"),
        #title_bottom: &title_bottom,
        &list(
            #highlight_style: &style(#fg: `Black, #bg: `Yellow),
            #highlight_symbol: &"â–¶ ",
            #repeat_highlight_symbol: &false,
            #selected: &selected,
            #scroll: &scroll_pos,
            #style: &style(#fg: `White),
            &items
        )
    )
)
